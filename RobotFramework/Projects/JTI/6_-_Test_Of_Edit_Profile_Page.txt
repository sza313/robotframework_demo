*** Settings ***
Suite Setup       Setup Test Environment
Suite Teardown    Close Browser
Resource          Keywords/functionalKeywords.robot

*** Test Cases ***
Log In
    [Documentation]    Log into the application with given credentials.
    ...    Expected - user logged in and redirected to index page.
    Log In    ${test user 1 name}    ${test user 1 pwd}

Navigate To Edit Profile Page
    [Documentation]    Click to Photo of user in right side of Top menu and choose Settings. Edit my profile site should be opened.
    Open Profile Menu
    Click Profile Menu Item    Settings
    Wait Until Element Contains    //h3[@class="panel-title"]    Profile

Click Profile Panel
    [Documentation]    Click to Profile in the left navigation.
    ...    Expected - Profile webpart is shown in top of window and button Profile in the left navigation is highlighted.
    Click Edit Profile Page Nav Link    Profile    Profile

Change Language
    [Documentation]    Click to "Your preferred language" dropdown in Profile webpart and change language to e.g.Deutsch.
    ...    Expected - The site is automatically reload and the language is changed to German.
    Go To    ${profile settings url}
    Wait Until Page Contains    CSTFERNBA
    Wait Until Page Contains Element    id:AccountLanguageVal
    Sleep    5 secs
    Select From List By Label    id:AccountLanguageVal    Deutsch
    Wait Until Page Contains    Ihre bevorzugte Sprache
    Select From List By Label    id:AccountLanguageVal    English
    Wait Until Page Contains    Your preferred language

Check About Text Length
    [Documentation]    Go to "About me" field and insert text that is longer than 240 characters.
    ...    Expected - Text limited to 240 characters.
    Clear Element Text    id:AccountAboutMeVal
    Input Text    id:AccountAboutMeVal    Insert this test to profile...Insert this test to profile...Insert this test to profile...Insert this test to profile..Insert this test to profile...Insert this test to profile..Insert this test to profile...Insert this test to profile...OKERROR    # "ERROR" should be cut off from the end of the text.
    Textarea Value Should Be    id:AccountAboutMeVal    Insert this test to profile...Insert this test to profile...Insert this test to profile...Insert this test to profile..Insert this test to profile...Insert this test to profile..Insert this test to profile...Insert this test to profile...OK

Change Settings In Profile And Save
    [Documentation]    Change any others details in profile and click save. Settings should be changed accordingly after page reload.
    Select From List By Label    id:AccountDefaultMarketVal    Canada
    Select From List By Label    id:AccountPrimaryDepartmentVal    Business Development
    Select From List By Label    id:AccountBirthDayDay    1
    Select From List By Value    id:AccountBirthDayMonth    7
    Clear Element Text    id:AccountInterestsVal
    Input Text    id:AccountInterestsVal    Photography
    Clear Element Text    id:AccountAboutMeVal
    Input Text    id:AccountAboutMeVal    My life for Ner'Zhul
    Save Profile Page
    List Selection Should Be    id:AccountDefaultMarketVal    Canada
    List Selection Should Be    id:AccountPrimaryDepartmentVal    Business Development
    List Selection Should Be    id:AccountBirthDayDay    1
    List Selection Should Be    id:AccountBirthDayMonth    7
    Textarea Value Should Be    id:AccountInterestsVal    Photography
    Textarea Value Should Be    id:AccountAboutMeVal    My life for Ner'Zhul
    Select From List By Label    id:AccountDefaultMarketVal    Hungary
    Select From List By Label    id:AccountPrimaryDepartmentVal    Finance
    Select From List By Label    id:AccountBirthDayDay    7
    Select From List By Value    id:AccountBirthDayMonth    3
    Clear Element Text    id:AccountInterestsVal
    Input Text    id:AccountInterestsVal    Music
    Clear Element Text    id:AccountAboutMeVal
    Input Text    id:AccountAboutMeVal    About Me
    Save Profile Page
    List Selection Should Be    id:AccountDefaultMarketVal    Hungary
    List Selection Should Be    id:AccountPrimaryDepartmentVal    Finance
    List Selection Should Be    id:AccountBirthDayDay    7
    List Selection Should Be    id:AccountBirthDayMonth    3
    Textarea Value Should Be    id:AccountInterestsVal    Music
    Textarea Value Should Be    id:AccountAboutMeVal    About Me

Click Expertise Panel
    [Documentation]    Click to Expertise in the left navigation.
    ...    Expected - Manage my expertise tags webpart is shown in top of window and button Expertise in the left navigation is highlighted.
    Click Edit Profile Page Nav Link    Expertise    Manage my expertise tags

Add Expertise Tag
    [Documentation]    Click to add topics and type text of item to field and select one of suggestions.
    Input Text    id:manageMyResponsibilities-addResponsibility-input    tag-${current timestamp}
    Press Key    id:manageMyResponsibilities-addResponsibility-input    \\13
    Wait Until Page Contains Element    //li[contains(@class,"pill") and ./span[text()="tag-${current timestamp}"]]
    Click Element    //li[contains(@class,"pill") and ./span[text()="tag-${current timestamp}"]]/span[@class="glyphicon glyphicon-close"]

Click News Panel
    [Documentation]    Click to News in the left navigation.
    ...    Expected - Manage my news tags webpart is shown in top of window and button News in the left navigation is highlighted
    Click Edit Profile Page Nav Link    News    Choose my news

Add News Tag
    [Documentation]    Click to text field "+add" and type any tag e.g. L&M -> Select tag from dropdown.
    ...    Expected - Tag was found and added.
    Input Text    id:myNewsTags    Lucky Strike
    Wait Until Keyword Succeeds    30 secs    0.5 secs    Add Suggested Tag    Lucky Strike    //div[@id="param_news"]
    Remove Tag    Lucky Strike

Check News Tabs
    [Documentation]    In tab Markets, Departments and Brands select any items.
    ...    Expected - Ticked items was added among tags according the rules (1st Markets, 2nd Departments, 3rd Brands).
    ${panel}=    Set Variable    //div[@id="param_news"]
    Scroll Element Into View    ${panel}//div[contains(@class,"FiltersTabNav")]
    Click Tab    Markets    ${panel}
    Select Check Box By Label    Andorra    ${panel}
    Check Tag By Name    Andorra    ${panel}
    Remove Tag    Andorra
    Click Tab    Departments    ${panel}
    Select Check Box By Label    Business Development    ${panel}
    Check Tag By Name    Business Development    ${panel}
    Remove Tag    Business Development
    Click Tab    Brands    ${panel}
    Select Check Box By Label    NAS    ${panel}
    Check Tag By Name    NAS    ${panel}
    Remove Tag    NAS
    Click Tab    Regions    ${panel}
    Select Check Box By Label    Global    ${panel}
    Check Tag By Name    Global    ${panel}
    Remove Tag    Global

Click Notifications Panel
    [Documentation]    Click to Notifications in the left navigation.
    ...    Expected - Notifications webpart is shown in top of window and button Notifications in the left navigation is highlighted.
    Click Edit Profile Page Nav Link    Notifications    Notifications

Change Settings In Notifications And Save
    [Documentation]    Change any details and click "Save changes". Data should be persistent after page reload.
    Scroll Element Into View    //input[@id="engageEmailDigestSwitch"]
    Select Checkbox    //input[@id="engageEmailDigestSwitch"]
    Scroll Element Into View    //input[@id="engageEmailDigestCommunitiesSwitch"]
    Select Checkbox    //input[@id="engageEmailDigestCommunitiesSwitch"]
    Scroll Element Into View    //input[@id="engageEmailDigestColleaguesSwitch"]
    Select Checkbox    //input[@id="engageEmailDigestColleaguesSwitch"]
    Scroll Element Into View    //input[@id="engageEmailDigestMyActivitiesSwitch"]
    Select Checkbox    //input[@id="engageEmailDigestMyActivitiesSwitch"]
    Scroll Element Into View    //input[@id="weeklyNotificationSwitch"]
    Select Checkbox    //input[@id="weeklyNotificationSwitch"]
    Scroll Element Into View    //select[@id="weeklyNotificationDayPicker"]
    Select From List By Label    //select[@id="weeklyNotificationDayPicker"]    Monday
    Scroll Element Into View    //input[@id="newsEmailDigestSwitch"]
    Select Checkbox    //input[@id="newsEmailDigestSwitch"]
    Scroll Element Into View    //select[@id="newsEmailDigestDayPicker"]
    Select From List By Label    //select[@id="newsEmailDigestDayPicker"]    Monday
    Save Profile Page
    Checkbox Should Be Selected    //input[@id="engageEmailDigestSwitch"]
    Checkbox Should Be Selected    //input[@id="engageEmailDigestCommunitiesSwitch"]
    Checkbox Should Be Selected    //input[@id="engageEmailDigestColleaguesSwitch"]
    Checkbox Should Be Selected    //input[@id="engageEmailDigestMyActivitiesSwitch"]
    Checkbox Should Be Selected    //input[@id="weeklyNotificationSwitch"]
    List Selection Should Be    //select[@id="weeklyNotificationDayPicker"]    Monday
    Checkbox Should Be Selected    //input[@id="newsEmailDigestSwitch"]
    List Selection Should Be    //select[@id="newsEmailDigestDayPicker"]    Monday
    Scroll Element Into View    //input[@id="engageEmailDigestSwitch"]
    Unselect Checkbox    //input[@id="engageEmailDigestSwitch"]
    Scroll Element Into View    //input[@id="engageEmailDigestCommunitiesSwitch"]
    Unselect Checkbox    //input[@id="engageEmailDigestCommunitiesSwitch"]
    Scroll Element Into View    //input[@id="engageEmailDigestColleaguesSwitch"]
    Unselect Checkbox    //input[@id="engageEmailDigestColleaguesSwitch"]
    Scroll Element Into View    //input[@id="engageEmailDigestMyActivitiesSwitch"]
    Unselect Checkbox    //input[@id="engageEmailDigestMyActivitiesSwitch"]
    Scroll Element Into View    //input[@id="weeklyNotificationSwitch"]
    Unselect Checkbox    //input[@id="weeklyNotificationSwitch"]
    Scroll Element Into View    //select[@id="weeklyNotificationDayPicker"]
    Select From List By Label    //select[@id="weeklyNotificationDayPicker"]    Friday
    Scroll Element Into View    //input[@id="newsEmailDigestSwitch"]
    Unselect Checkbox    //input[@id="newsEmailDigestSwitch"]
    Scroll Element Into View    //select[@id="newsEmailDigestDayPicker"]
    Select From List By Label    //select[@id="newsEmailDigestDayPicker"]    Friday
    Save Profile Page
    Checkbox Should Not Be Selected    //input[@id="engageEmailDigestSwitch"]
    Checkbox Should Not Be Selected    //input[@id="engageEmailDigestCommunitiesSwitch"]
    Checkbox Should Not Be Selected    //input[@id="engageEmailDigestColleaguesSwitch"]
    Checkbox Should Not Be Selected    //input[@id="engageEmailDigestMyActivitiesSwitch"]
    Checkbox Should Not Be Selected    //input[@id="weeklyNotificationSwitch"]
    List Selection Should Be    //select[@id="weeklyNotificationDayPicker"]    Friday
    Checkbox Should Not Be Selected    //input[@id="newsEmailDigestSwitch"]
    List Selection Should Be    //select[@id="newsEmailDigestDayPicker"]    Friday

Click Colleagues Panel
    [Documentation]    Click to Colleagues in the left navigation.
    ...    Expected - Colleagues webpart is shown in top of window and button Colleagues in the left navigation is highlighted.
    Click Edit Profile Page Nav Link    Colleagues    Colleagues

Add A Colleague
    [Documentation]    Add a colleague via the popup in Colleagues panel. Check if colleague added after page reload.
    Click Link Which Contains    Add Colleagues
    Wait Until Dialog Is Open    Add Colleagues
    ${add colleagues dialog}=    Set Variable    //div[@role="dialog" and .//h1[contains(text(),"Add Colleagues")]]
    Select Frame    ${add colleagues dialog}//iframe
    Click Element    //a[@title="Browse"]
    Unselect Frame
    ${select people dialog}=    Set Variable    //div[@role="dialog" and .//h1[contains(text(),"Select People")]]
    Wait Until Dialog Is Open    Select People
    Wait Until Page Contains Element    ${select people dialog}//iframe
    Select Frame    ${select people dialog}//iframe
    Wait Until Page Contains Element    //input[contains(@id,"queryTextBox")]
    Input Text    //input[contains(@id,"queryTextBox")]    ${test user 2 full name}
    Press Key    //input[contains(@id,"queryTextBox")]    \\13
    Wait Until Page Contains Element    //tr[./td[contains(text(),"${test user 2 full name}")]]
    Click Element    //tr[./td[contains(text(),"${test user 2 full name}")]]
    Click Element    //input[contains(@name,"AddSel")]
    Click Element    //input[contains(@name,"btnOk")]
    Unselect Frame
    Select Frame    ${add colleagues dialog}//iframe
    Click Element    //input[contains(@name,"OkButton")]
    Unselect Frame
    Wait Until Page Contains Element    //a[text()="${test user 2 full name}"]

Remove A Colleague
    [Documentation]    Tick the checkbox near of any colleague and click to Remove colleagues. Colleague should be removed from the list.
    Scroll Element Into View    //a[text()="${test user 2 full name}"]/ancestor::tr//input[@type="checkbox"]
    Select Checkbox    //a[text()="${test user 2 full name}"]/ancestor::tr//input[@type="checkbox"]
    Click Link Which Contains    Remove Colleagues
    Handle Alert    ACCEPT
    Wait Until Page Does Not Contain Element    //a[text()="${test user 2 full name}"]/ancestor::tr
