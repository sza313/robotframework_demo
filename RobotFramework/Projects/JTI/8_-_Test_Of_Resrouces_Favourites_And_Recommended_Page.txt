*** Settings ***
Suite Setup       Setup Test Environment
Suite Teardown    Close Browser
Resource          Keywords/functionalKeywords.robot

*** Test Cases ***
Log In
    [Documentation]    Log into the application with given credentials.
    ...    Expected - user logged in and redirected to index page.
    Log In    ${test user 1 name}    ${test user 1 pwd}

Navigate To Favourites Page
    [Documentation]    Navigate to Favourites page by clicking on main menu item "Resources", and by selecting "Favourites" from "My Pages" dropdown.
    Click Main Menu Item    Resources
    Wait Until Page Contains    My favorites
    Wait Until Page Contains Element    //a[@class="active" and text()="My favorites"]
    Go To    ${index page url}
    Click Main Menu Item    My pages
    Wait Until Page Contains Link With Text    See All
    Click Link Which Contains    See All
    Sleep    5
    Wait Until Page Contains    My favorites
    Wait Until Page Contains Element    //a[@class="active" and text()="My favorites"]

Check My Favorites Page
    [Documentation]    Check If My favorites page parts are loaded corectly.
    Check Resources Page Left Sidebar
    Wait Until Page Contains Span With Text    My applications and sites
    Wait Until Page Contains Element    //span[text()="My applications and sites"]/following-sibling::div[@class="library-content"]//div[@class="item"]
    Check Resources Page Pagination    //span[text()="My applications and sites"]/following-sibling::div[@class="library-content"]
    Wait Until Page Contains Span With Text    My saved documents
    Wait Until Page Contains Element    //span[text()="My saved documents"]/following-sibling::div[@class="library-content"]//div[@class="item"]
    Check Resources Page Pagination    //span[text()="My saved documents"]/following-sibling::div[@class="library-content"]
    Filter Favorites Library By Filters    //div[@class="stm-library saved-docs"]
    Wait Until Page Contains Element    //h3[text()="My pages"]
    Check External Links    //div[@class="my-pages"]

Check Recommended Page
    [Documentation]    Click "Recommended" link in left navigation and check if contents are loaded properly.
    Click Sidebar Nav Link    Recommended
    Wait Until Page Contains    Recommended for you
    Check Resources Page Left Sidebar
    Wait Until Page Contains Span With Text    Recommended applications and sites
    Check Resources Page Pagination    //span[text()="Recommended applications and sites"]/../div[@class="library-content"]
    Wait Until Page Contains Element    //span[text()="Recommended applications and sites"]/following-sibling::div[@class="library-content"]//div[@class="item"]
    Wait Until Page Contains Span With Text    Recommended documents
    Check Resources Page Pagination    //span[text()="Recommended documents"]/../div[@class="library-content"]
    Wait Until Page Contains Element    //span[text()="Recommended documents"]/following-sibling::div[@class="library-content"]//div[@class="item"]
    Filter Favorites Library By Filters    //div[@class="stm-library saved-docs"]
    Wait Until Page Contains Element    //h3[text()="Recommended"]
    Check External Links    //h3[text()="Recommended"]

Check Applications And Sites Page
    Check Resources Page    Applications & sites

Check Guidelines And Toolkits Page
    Check Resources Page    Guidelines & toolkits

Check Policies And Procedures Page
    Check Resources Page    Policies & procedures

Check Reports And Case Studies Page
    Check Resources Page    Reports & case studies

Check Templates Page
    Check Resources Page    Templates

Check Training Page
    Check Resources Page    Training
